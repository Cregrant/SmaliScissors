plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = 1.7
}

compileTestJava {
    sourceCompatibility = 1.8
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.36'

    testImplementation 'ch.qos.logback:logback-classic:1.2.11'
    testImplementation 'org.apache.commons:commons-compress:1.22'
    testRuntimeOnly 'org.tukaani:xz:1.9'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.9.0'
    testCompileOnly 'junit:junit:4.13.2'
}

test {
    useJUnitPlatform()
    jvmArgs = ['-Xms512m', '-Xmx4096m']
}

task fatJar(type: Jar) {
    archiveBaseName.set("patcher")
    destinationDirectory.set(rootDir)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    delete(archiveFile.get())

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    } with jar

    from('..') {
        include 'LICENSE.txt'
        include 'NOTICE.txt'
        into 'META-INF/SmaliScissors/'
    }
}
